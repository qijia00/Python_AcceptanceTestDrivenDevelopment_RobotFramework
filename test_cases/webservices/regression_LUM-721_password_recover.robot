*** Settings ***
Library          WebServiceLibrary
Library          ECULibrary
Library          BuiltIn
Library          OperatingSystem

Suite Setup      Setup System
Suite Teardown   Breakdown System

Force Tags       webservices   LUM-721   password_recovery   semi-automated   regression

*** Test Cases ***
Challenge and Recovery key is not allowed on blank ECU
    connect to web services   ${BLANK_ECU_IP}
    RUN KEYWORD AND EXPECT ERROR   FAILED: Operation requires MasterECU privilege!   get new challenge key   ${user}
    RUN KEYWORD AND EXPECT ERROR   FAILED: Operation requires MasterECU privilege!   get challenge key
    RUN KEYWORD AND EXPECT ERROR   FAILED: Operation requires MasterECU privilege!   post recovery key   ${user}   123456

Challenge key is allowed on Master ECU
    connect to web services   ${master_ECU_IP}
    # If user doesn't exist, challenge key should be returned anyway... to avoid leak of username in event of brute force attack.
    ${new_challenge_key}=   get new challenge key   wrong_user_name
    Log   ${new_challenge_key}
    ${challenge_key}=   get challenge key
    Log   ${challenge_key}

    # There needs to be a minimum time of 1 minute between Challenge Key requests
    RUN KEYWORD AND EXPECT ERROR   FAILED: Challenge-Key generated is less than one minute old, please use the generated one!   get new challenge key   ${user}
    ${challenge_key_2}=   get challenge key
    Log   ${challenge_key_2}
    should be equal   ${challenge_key}   ${challenge_key_2}

    # ${new_challenge_key_2} will result ${new_challenge_key} expire
    sleep   60s
    ${new_challenge_key_2}=   get new challenge key   ${user}
    Log   ${new_challenge_key_2}
    should not be equal   ${new_challenge_key}   ${new_challenge_key_2}
    ${challenge_key_3}=   get challenge key
    Log   ${challenge_key_3}
    should not be equal   ${challenge_key}   ${challenge_key_3}

Recovery Key is allowed on Master ECU
    connect to web services   ${master_ECU_IP}
    # ${new_challenge_key_2} generated by ${user} will result ${new_challenge_key} generated by 'wrong_user_name' expire
    RUN KEYWORD AND EXPECT ERROR   FAILED: Username: 'wrong_user_name' does not correspond to the user who generated the challenge-key!   post recovery key   wrong_user_name   123456
    # minimum of 1 minute between /api/user-recovery-password::POST calls is required.
    RUN KEYWORD AND EXPECT ERROR   FAILED: Recovery-key reponsed less than one minute ago!   post recovery key   ${user}   123456
    sleep   60s
    Log   below will fail due to LUM-4226   WARN
    RUN KEYWORD AND EXPECT ERROR   FAILED: Recovery-Key is not correct!   post recovery key   ${user}   123456
    sleep   60s

    # generate valid recovery key for ${new_challenge_key_2}
    ${challenge_key}=   get challenge key
    ${recovery_key}=   generate recovery key   ${challenge_key}   ${challenge_key_file_path}   ${batch_file_folder_path}   ${recovery_key_file}
    set global variable   ${recovery_key_global}   ${recovery_key}
    clean sessions
    Log   below will fail due to LUM-4226   WARN
    post recovery key   ${user}    ${recovery_key_global}
    # if Recovery Key is valid then user is granted valid session-ID otherwise error code is returned
    get registration offset
    get user info
    # a valid Recovery Key does NOT expire the Challenge Key
    ${challenge_key}=   get challenge key
    Log   ${challenge_key}

Challenge Key expires after 7 days
    establish ssh connection   ${master_ECU_IP}
    extend linux clock   day=7
    sleep   60s

    connect to web services   ${master_ECU_IP}
    #After the Challenge key is generated, user needs to enter the Recovery key within 7 days.
    clean sessions
    RUN KEYWORD AND EXPECT ERROR   FAILED: Challenge-key not generated or expired!   post recovery key   ${user}    ${recovery_key_global}

Original password for user still works when Challenge Key is outstanding
    connect to web services   ${master_ECU_IP}
    login   ${user}   ${pass}
    reboot ecu   # bring the ECU time back to normal

*** Variables ***
${version}   v2
${site_type}   lumenade
${blank_ECU_IP}   172.24.172.101
${master_ECU_IP}   172.24.172.102
${user}   sysadmin
${def_pass}   1um3nad3
${pass}   newpassword

${new_site}   SEPARATOR=\n
...     {
...         "name": "Site management test",
...         "version": "Version 4.0.0",
...         "customer": "QA",
...         "project": "Robot",
...         "author": "Jia",
...         "default": true,
...         "date": "06/29/2017 9:54:00 AM",
...         "username": "${user}",
...         "password": "${pass}",
...         "fullname": "System Administrator",
...         "site-type": "${site_type}"
...     }

${challenge_key_file_path}   .//input//tool_to_create_recovery_key//text_id.txt
${batch_file_folder_path}   .//input//tool_to_create_recovery_key
${recovery_key_file}   text_id.sig_e64
${recovery_key_global}

*** Keywords ***
Setup System
    run keyword and ignore error   Connect to web services   ${master_ECU_IP}   ${user}   ${def_pass}   ${version}
    run keyword and ignore error   Connect to web services   ${master_ECU_IP}   ${user}   ${pass}   ${version}
    Run keyword and ignore error   Remove from site
    Run keyword and ignore error   Get database information
    Run keyword and ignore error   Delete site with id   SITE0
    Create new site   ${new_site}
    Logout
    Login   ${user}   ${pass}
    Get database information
    Bring ECU into site   ${master_ECU_IP}   ecu_name=MasterECU   site_type=${site_type}
    Logout

Breakdown System
    run keyword and ignore error   Connect to web services   ${master_ECU_IP}   ${user}   ${pass}   ${version}
    run keyword and ignore error   Connect to web services   ${master_ECU_IP}   ${user}   ${def_pass}   ${version}
    Run keyword and ignore error   Remove from site
    Get database information
    Delete site with id   SITE0
    Logout
    Remove File   .//artifacts//*.sqlite
    Remove File   .//input//tool_to_create_recovery_key//*.txt
    Remove File   .//input//tool_to_create_recovery_key//*.sig
    Remove File   .//input//tool_to_create_recovery_key//*.sig_e64